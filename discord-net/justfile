ARCH := "x86_64"
OUTDIR := "./bin"
FLAGS := "--sc"

all: install

clean:
  rm -rf ./**/obj/
  rm -rf ./**/bin/
  rm -f ./**/compile_commands.json
  rm -rf ./Native/.zig-cache/
  rm -rf ./Native/zig-out/
  rm -f ./Native/bindings/*.cs
  rm -f ./**/*_wrap.cxx
  rm -f ./**/__*.zig
  dotnet clean

# lock: configure
#   #!/usr/bin/env nu
#   rm -f ./packages.lock.json
#   open ./**/packages.lock.json | reduce --fold {} {|item, acc| $acc | merge $item} | save packages.lock.json
[working-directory: "./Native"]
zon2nix:
  zon2nix > ./build.zig.zon.nix

swig:
  mkdir -p ./Native/bindings/
  rm -f ./Native/bindings/*.cs
  rm -f ./Native/swig_wrap.cxx
  swig -csharp -c++ -outdir ./Native/bindings -namespace Native -v ./Native/swig.i

configure: swig
  rm -f *.sln
  rm -f *.slnx
  dotnet new sln
  dotnet sln add ./**/*.csproj
  dotnet restore
  cd ./Native # && zig build cdb

native: swig
  cd ./Native && zig build --release=fast -Dtarget={{ARCH}}-linux-musl --verbose

build: swig
  dotnet build ./RuTakingTooLong/RuTakingTooLong.csproj -c Release

install-native: native
  mkdir -p "{{OUTDIR}}"
  cp ./Native/zig-out/lib/** "{{OUTDIR}}"

install-dotnet: configure build
  mkdir -p "{{OUTDIR}}"
  dotnet publish -c Release -o "{{OUTDIR}}" "{{FLAGS}}" ./RuTakingTooLong/RuTakingTooLong.csproj

install: install-native install-dotnet

run: install
  dotnet ./bin/RuTakingTooLong.dll

